\chapter{No Time for a Taxicab}\label{no-time-for-a-taxicab}

\href{https://adventofcode.com/2016/day/1}{Link}

Santa's sleigh uses a very high-precision clock to guide its movements,
and the clock's oscillator is regulated by stars. Unfortunately, the
stars have been stolen\ldots{} by the Easter Bunny. To save Christmas,
Santa needs you to retrieve all \textbf{fifty stars} by December 25th.

Collect stars by solving puzzles. Two puzzles will be made available on
each day in the advent calendar; the second puzzle is unlocked when you
complete the first. Each puzzle grants \textbf{one star}. Good luck!

You're airdropped near \emph{Easter Bunny Headquarters} in a city
somewhere. ``Near'', unfortunately, is as close as you can get - the
instructions on the Easter Bunny Recruiting Document the Elves
intercepted start here, and nobody had time to work them out further.

The Document indicates that you should start at the given coordinates
(where you just landed) and face North. Then, follow the provided
sequence: either turn left (\mintinline[]{idris}{L}) or right
(\mintinline[]{idris}{R}) 90 degrees, then walk forward the given number
of blocks, ending at a new intersection.

There's no time to follow such ridiculous instructions on foot, though,
so you take a moment and work out the destination. Given that you can
only walk on the
\href{https://en.wikipedia.org/wiki/Taxicab_geometry}{street grid of the
city}, how far is the shortest path to the destination?

For example:

\begin{itemize}
\tightlist
\item
  Following \mintinline[]{idris}{R2, L3} leaves you
  \mintinline[]{idris}{2} blocks East and \mintinline[]{idris}{3} blocks
  North, or \mintinline[]{idris}{5} blocks away.
\item
  \mintinline[]{idris}{R2, R2, R2} leaves you \mintinline[]{idris}{2}
  blocks due South of your starting position, which is
  \mintinline[]{idris}{2} blocks away.
\item
  \mintinline[]{idris}{R5, L5, R5, R3} leaves you
  \mintinline[]{idris}{12} blocks away.
\end{itemize}

\newpage

\section{Module Declaration and
Imports}\label{module-declaration-and-imports}

\begin{minted}[]{idris}
||| Day 1: No Time for a Taxicab
module Data.Advent.Day01
\end{minted}

Import the
\href{https://github.com/yurrriq/advent-of-idris/blob/master/src/Data/Advent/Day.idr}{generic
\mintinline[]{idris}{Day} structure}, inspired by
\href{https://github.com/purcell/adventofcode2016}{Steve Purcell Haskell
solution}.

\begin{minted}[]{idris}
import public Data.Advent.Day
\end{minted}

For no compelling reason, I like to use
\href{https://www.haskell.org/arrows/}{arrows}, so import the requisite
modules.

\begin{minted}[]{idris}
import Control.Arrow
import Control.Category
import Data.Morphisms
import Data.SortedSet
\end{minted}

For parsing, use \href{https://github.com/ziman/lightyear}{Lightyear}.

\begin{minted}[]{idris}
import public Lightyear
import public Lightyear.Char
import public Lightyear.Strings
\end{minted}

\section{Data Types}\label{data-types}

Ensure both the type and data constructors are
\href{http://docs.idris-lang.org/en/latest/tutorial/modules.html\#meaning-for-data-types}{exported}
for all the following data types.

\begin{minted}[]{idris}
%access public export
\end{minted}

We'll need to keep track of which cardinal direction we're facing, so
define a data type \mintinline[]{idris}{Heading} to model that. A
heading is \mintinline[]{idris}{N}orth, \mintinline[]{idris}{E}ast,
\mintinline[]{idris}{S}outh or \mintinline[]{idris}{W}est.

\begin{minted}[]{idris}
||| A cardinal heading.
data Heading = ||| North
               N
             | ||| East
               E
             | ||| South
               S
             | ||| West
               W
\end{minted}

As per the \href{no-time-for-a-taxicab}{puzzle description}, the
instructions will tell us to go \mintinline[]{idris}{L}eft or
\mintinline[]{idris}{R}ight, so model \mintinline[]{idris}{Direction}s
accordingly.

\begin{minted}[]{idris}
||| A direction to walk in, left or right.
data Direction = ||| Left
                 L
               | ||| Right
                 R
\end{minted}

\newpage

For convenience in the REPL, implement the \mintinline[]{idris}{Show}
\href{http://docs.idris-lang.org/en/latest/tutorial/interfaces.html}{interface}
for \mintinline[]{idris}{Direction}.

\begin{minted}[]{idris}
implementation Show Direction where
    show L = "Left"
    show R = "Right"
\end{minted}

An \mintinline[]{idris}{Instruction}, e.g. \mintinline[]{idris}{R2}, is
comprised of a \mintinline[]{idris}{Direction} and a number of blocks to
walk in that direction.

As such, define an \mintinline[]{idris}{Instruction} as a record
containing a \mintinline[]{idris}{Direction} and a number of
\mintinline[]{idris}{Blocks}.

\begin{minted}[]{idris}
Blocks : Type
Blocks = Integer

||| A direction and a number of blocks.
record Instruction where
    constructor MkIns
    iDir    : Direction
    iBlocks : Blocks
\end{minted}

A \mintinline[]{idris}{Position} is a record containing x and y
coordinates in the
\href{https://en.wikipedia.org/wiki/Taxicab_geometry}{street grid}.

\begin{minted}[]{idris}
||| A pair of coordinates on the street grid.
record Position where
    constructor MkPos
    posX, posY : Blocks

implementation Eq Position where
    (MkPos x1 y1) == (MkPos x2 y2) = x1 == x2 && y1 == y2

implementation Ord Position where
    compare (MkPos x1 y1) (MkPos x2 y2) with (compare x1 x2)
      | EQ = compare y1 y2
      | o  = o
\end{minted}

\section{Parsers}\label{parsers}

Ensure the type constructors of the following parsers are
\href{http://docs.idris-lang.org/en/latest/tutorial/modules.html\#meaning-for-data-types}{exported}.

\begin{minted}[]{idris}
%access export
\end{minted}

A \mintinline[]{idris}{Direction} is represented in the input by a
single character, \mintinline[]{idris}{'L'} or
\mintinline[]{idris}{'R'}.

\begin{minted}[]{idris}
direction : Parser Direction
direction = (char 'L' *> pure L) <|>|
            (char 'R' *> pure R) <?>
            "a direction"
\end{minted}

\newpage

An \mintinline[]{idris}{Instruction} is represented as a
\mintinline[]{idris}{Direction} followed immediately by an integer.

\begin{minted}[]{idris}
||| Parse an instruction, i.e. a direction and
||| a number of blocks to walk in that direction.
partial instruction : Parser Instruction
instruction = [| MkIns direction integer |] <?> "an instruction"
\end{minted}

The instructions are given as a comma-separated list.

\begin{minted}[]{idris}
||| Parse a comma-separated list of `Instruction`s.
partial instructions : Parser (List Instruction)
instructions = commaSep instruction <* spaces <?>
               "a comma-separated list of instructions"
\end{minted}

\section{Logic}\label{logic}

\begin{minted}[]{idris}
||| Return the new heading after turning right.
||| @ initial the initial heading
turnRight : (initial : Heading) -> Heading
turnRight N = E
turnRight E = S
turnRight S = W
turnRight W = N

||| Return the new heading after turning left.
||| @ initial the initial heading
turnLeft : (initial : Heading) -> Heading
turnLeft N = W
turnLeft E = N
turnLeft S = E
turnLeft W = S

||| Return the new heading after turning in a given direction.
turn : Heading -> Direction -> Heading
turn h L = turnLeft  h
turn h R = turnRight h

||| Return the new position after moving one block with a given heading.
move : Heading -> (Position -> Position)
move N = record { posY $= (+ 1) }
move E = record { posX $= (+ 1) }
move S = record { posY $= (flip (-) 1) }
move W = record { posX $= (flip (-) 1) }
\end{minted}

\begin{minted}[]{idris}
||| Compute the shortest distance from the initial position and a given one.
shortestDistance : Position -> Blocks
shortestDistance (MkPos x y) = abs x + abs y
\end{minted}

\newpage

\begin{minted}[]{idris}
||| Return the list of intermediate positions visited
||| when following a given list of instructions.
followInstructions : List Instruction -> List Position
followInstructions is =
    let directions = drop 1 $ scanl turn N (iDir <$> is)
        distances  = iBlocks <$> is
        moves      = concatMap expand (zip directions distances) in
        scanl (flip move) (MkPos 0 0) moves
  where
    expand : (Heading, Blocks) -> List Heading
    expand (h, n) = replicate (cast n) h
\end{minted}

\section{Part One}\label{part-one}

\begin{quote}
  \textit{How many blocks away} is Easter Bunny HQ?
\end{quote}

To compute the answer to Part One, \mintinline[]{idris}{262}, follow the
instructions, get the last position and compute the shortest distance
from the initial position.

\begin{minted}[]{idris}
partOne : List Instruction -> Maybe Blocks
partOne = map shortestDistance . last' . followInstructions
\end{minted}

\section{Part Two}\label{part-two}

Then, you notice the instructions continue on the back of the Recruiting
Document. Easter Bunny HQ is actually at the first location you visit
twice.

For example, if your instructions are
\mintinline[]{idris}{R8, R4, R4, R8}, the first location you visit twice
is \mintinline[]{idris}{4} blocks away, due East.

\begin{quote}
  How many blocks away is the \textit{first location you visit twice}?
\end{quote}

First, define a function \mintinline[]{idris}{firstDuplicate} to find
the first location visited twice in a list of positions. Instead of
specializing for \mintinline[]{idris}{List Position}, generalize the
solution to \mintinline[]{idris}{Ord a => List a}.

\begin{minted}[]{idris}
||| Return `Just` the first duplicate in a given list.
||| If there are none, return `Nothing`.
firstDuplicate : Ord a => List a -> Maybe a
firstDuplicate = go empty
  where
    go : SortedSet a -> List a -> Maybe a
    go _ []         = Nothing
    go seen (x::xs) =
        if contains x seen
           then Just x
           else go (insert x seen) xs
\end{minted}

\begin{minted}[]{idris}
partTwo : List Instruction -> Maybe Blocks
partTwo = map shortestDistance . firstDuplicate . followInstructions
\end{minted}

\newpage

\section{Main}\label{main}

Run the solutions for Day 1.

\begin{minted}[]{idris}
namespace Main

    partial main : IO ()
    main = runDay $ MkDay 1 instructions
           (pure . maybe "Failed!" show . partOne)
           (pure . maybe "Failed!" show . partTwo)
\end{minted}

\(\qed\)

\newpage
